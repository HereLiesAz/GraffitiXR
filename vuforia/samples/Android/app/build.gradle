plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

def VUFORIA_ENGINE_PATH = '../../../..'

// This file includes directives to enable the use of ARCore APIs in the App
// These directives are not necessary to use Vuforia. They are only required if
// the App will be using additional ARCore capabilities not exposed by Vuforia.

// The path to extract the ARCore native libraries to
// Part of enabling use of ARCore APIs in the App
def ARCORE_LIBPATH = "${buildDir}/arcore-native"

// Create a configuration to mark which aars to extract .so files from
// Part of enabling use of ARCore APIs in the App
configurations { natives }

android {
    namespace "com.vuforia.engine.native_sample"

    defaultConfig {
        compileSdk 35
        targetSdk 35
        minSdk 29
        ndkVersion "28.1.13356709"

        versionCode 1
        versionName '11.4.4'

        ndk {
            // VuforiaEngine supports only arm64-v8a architecture on Android
            abiFilters 'arm64-v8a'
        }
        externalNativeBuild {
            cmake {
                cppFlags "-std=c++17"

                // Pass ARCore paths to cmake
                // Part of enabling use of ARCore APIs in the App
                arguments "-DARCORE_LIBPATH=${ARCORE_LIBPATH}/jni",
                        "-DARCORE_INCLUDE=${project.rootDir}/arcore/include"
            }
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = "17"
    }

    archivesBaseName = "vuforia-native-sample"
    sourceSets {
        main {
            assets.srcDirs += ['../../Assets/ImageTargets','../../Assets/ModelTargets']
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
        }
    }
    buildFeatures {
        viewBinding true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4'

    implementation files("$VUFORIA_ENGINE_PATH/build/java/VuforiaEngine.jar")

    implementation 'com.google.ar:core:1.45.0' // Add ARCore support
    natives 'com.google.ar:core:1.45.0' // Enabling use of ARCore APIs in the App
}

// Extract the shared libraries from aars in the natives configuration.
// This is done so that NDK builds can access these libraries.
// Method added as part of enabling use of ARCore APIs in the App
tasks.register('extractNativeLibraries') {
    // Always extract, this insures the native libs are updated if the version changes.
    outputs.upToDateWhen { false }
    doFirst {
        configurations.natives.files.each { f ->
            copy {
                from zipTree(f)
                into ARCORE_LIBPATH
                include "jni/**/*"
            }
        }
    }
}

// Method added as part of enabling use of ARCore APIs in the App
tasks.configureEach {
    task -> if ((task.name.contains("external") || task.name.contains("CMake")) && !task.name.contains("Clean")) {
        task.dependsOn(extractNativeLibraries)
    }
}

// Add a wrapper task so that this project can be imported into Android Studio
tasks.register('wrapper', Wrapper) {
    gradleVersion = "latest"
}
